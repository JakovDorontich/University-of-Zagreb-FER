				B GLAVNI
					
				ORG 18
PP				STMFD R13!, {R0, R1, R2, R3, R14}	
				
				LDR R3, RTC			; R3 = RTC ADRESA
				LDR R2, GPIO		; R2 = GPIO ADRESA
				
				MOV R0, #0
				STR R0, [R2, #8]	; PRIHVAT PREKIDA
				STR R0, [R2, #0C]	; RESETIRANJE BROJILA
				
				MOV R0, #0D
				BL LCDWR			; RESETIRANJE LCD-A
				
				LDR R1, INDEX		
				LDRB R1, [R1]		; R1 = UCITANI ZNAK
				CMP R1, #0			; KRAJ NIZA ?
				BEQ PP_KRAJ1	
				
				MOV R0, #3E			; R0 = ZNAK '>'
				BL LCDWR
				BL LCDWR
				BL LCDWR
				
				MOV R0, R1			; R0 = UCITANI ZNAK
				BL LCDWR
				
				MOV R0, #03C		; R0 = ZNAK '<'
				BL LCDWR
				BL LCDWR
				BL LCDWR
				
				MOV R0, #0A
				BL LCDWR			; ISPIS
		
				B PP_KRAJ2
		
PP_KRAJ1		MOV R1, #40<4	
				STR R1, INDEX		; RESETIRANJE POKAZIVACA NA NIZ
				
				B PP_KRAJ
				
PP_KRAJ2		LDR R1, INDEX		; PRIPREMA ZA SLJEDECE UCITAVANJE ZNAKA
				ADD R1, R1, #1
				STR R1, INDEX
				
PP_KRAJ			LDMFD R13!, {R0, R1, R2, R3, R14}
				SUBS PC, LR, #4		; RETURN
				
GLAVNI			MOV R13, #12<8		; STOG_INIT -> ADR: 1200
				
				MRS R1, CPSR		
				BIC R1, R1, #80		; OMOGUCAVANJE PREKIDA NA ARM-U
				MSR CPSR_C, R1
				
				LDR R2, RTC			; R2 = RTC ADRESA
				MOV R0, #1
				STR R0, [R2, #10]	; OMOGUCAVANJE PRIKIDA NA RTC-U
				MOV R0, #1<8		; R0 = 256(10)
				STR R0, [R2, #4]	; REGISTAR USPOREDBE
				MOV R0, #0
				STR R0, [R2, #0C]	; PUNJENJE BROJILA
				
PETLJA			B PETLJA			; BESKONACNA PETLJA

RTC				DW 0FFFFFE00		; RTC ADRESA
GPIO			DW 0FFFFFF00		; GPIO ADRESA
INDEX			DW 400				

LCDWR			STRB R0, [R2, #4]    ; PUNJENJE REGISTRA
				
				ORR R0, R0, #80		; GENERIRANJE IMPULSA
				STRB R0, [R2, #4]
				AND R0, R0, #7F	
				STRB R0, [R2, #4]	
				
				MOV PC, LR
				
				ORG 400
PODATCI			DB 49, 6E, 74, 65, 72, 6E, 61, 74, 69, 6F, 6E, 61, 6C, 69, 73, 61, 74, 69, 6F, 6E, 0

